apply plugin: 'com.android.application'
//apply plugin: 'com.antfortune.freeline'

def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))
}

def loadSigningConfigs() {
    def Properties props = new Properties()
    def propFile = file('../signing.properties')
    if (propFile.canRead()) {
        props.load(new FileInputStream(propFile))
        if (props != null && props.containsKey('RELEASE_STORE_FILE') && props.containsKey('RELEASE_STORE_PASSWORD') &&
                props.containsKey('RELEASE_KEY_ALIAS') && props.containsKey('RELEASE_KEY_PASSWORD')) {
            android.signingConfigs.release.storeFile = file(props['RELEASE_STORE_FILE'])
            android.signingConfigs.release.storePassword = props['RELEASE_STORE_PASSWORD']
            android.signingConfigs.release.keyAlias = props['RELEASE_KEY_ALIAS']
            android.signingConfigs.release.keyPassword = props['RELEASE_KEY_PASSWORD']
        } else {
            android.buildTypes.release.signingConfig = null
        }
    } else {
        android.buildTypes.release.signingConfig = null
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.0'
    defaultConfig {
        applicationId "com.pax.edc"
        minSdkVersion 19
        targetSdkVersion 19
        versionCode 1
        versionName "1.00.00"
    }

    lintOptions {
        abortOnError false
    }

    //签名
    signingConfigs {
        debug {
        }
        release {
        }
    }
    buildTypes {
        debug {
            // 显示Log
            buildConfigField "boolean", "LOG_ENABLE", "true"
            buildConfigField "String", "CARDNUMBER", "\"4336680005805219\""
            versionNameSuffix "-debug"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
        }
        release {
            // 不显示Log
            buildConfigField "boolean", "LOG_ENABLE", "false"
            buildConfigField "String", "CARDNUMBER", ""
            //混淆
            minifyEnabled true
            //Zipalign优化
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            //前一部分代表系统默认的android程序的混淆文件，该文件已经包含了基本的混淆声明，后一个文件是自己的定义混淆文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            loadSigningConfigs()

            //签名
            signingConfig signingConfigs.release

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        def type = ""
                        if (variant.buildType.name == 'debug') {
                            type = "_debug"
                        }
                        def fileName = "PAX_EDC(A)_V${defaultConfig.versionName}_${releaseTime()}${type}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    productFlavors {
    }
}

dependencies {
    compile 'com.android.support:support-v4:25.1.1'
    //编译libs目录下所有jar包
    compile fileTree(include: ['*.jar'], dir: 'libs')
    //首页轮播广告图banner
    compile 'cn.bingoogolapple:bga-banner:2.1.7'
    //图片加载框架
    compile 'com.github.bumptech.glide:glide:3.7.0'
    //消息总线，用于替换handler、intent等
    compile 'org.greenrobot:eventbus:3.0.0'
    //ButterKnife注解，主要用于代替findViewById以及view.setOnClickListener
    compile 'com.jakewharton:butterknife:8.5.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.5.1'
    //内存泄露检测，只有在debug版本会开启检测
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'

    debugCompile 'io.github.skyhacker2:sqliteonweb:1.0.2'
    releaseCompile 'io.github.skyhacker2:sqliteonweb-no-op:1.0.2'

    compile project(':slideexpandable')
}
